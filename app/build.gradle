plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp' version '1.7.20-1.0.7'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk versions.ongoing.sdk.compile
    flavorDimensions dimensions.mode.name

    defaultConfig {
        applicationId 'com.jeanbarrossilva.ongoing'
        minSdk versions.ongoing.sdk.min
        targetSdk versions.ongoing.sdk.target
        versionCode versions.ongoing.code
        versionName versions.ongoing.name

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    productFlavors {
        clean {
            dimension dimensions.mode.name
        }

        demo {
            dimension dimensions.mode.name
            applicationIdSuffix dimensions.mode.flavors.demo.suffix
        }
    }

    sourceSets {
        clean.java.srcDir 'src/shared/java'
        demo.java.srcDir 'src/shared/java'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

    kotlinOptions {
        jvmTarget = versions.java.toString()
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

dependencies {
    implementation project(':context-registry')
    implementation project(':core-session-in-memory')
    implementation project(':feature-settings')
    implementation project(':feature-activities')
    implementation project(':feature-activity-details')
    implementation project(':feature-activity-editing')
    implementation project(':feature-authentication')
    implementation project(':platform-design-system')
    implementation project(':platform-extensions')
    implementation project(':platform-registry')
    implementation libraries.koin.androidx_compose
    implementation libraries.material
    implementation libraries.navigation.compose

    androidTestImplementation project(':core-registry-in-memory')
    androidTestImplementation project(':platform-registry-test')
    androidTestImplementation libraries.compose.ui.test.junit4
    androidTestImplementation libraries.test.ext.junit
}